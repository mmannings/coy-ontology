PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sw: <http://www.w3.org/2003/06/sw-vocab-status/ns#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX void: <http://rdfs.org/ns/void#>
construct {
  ?class sw:term_status "See subclasses" .
}
where {
  ?class rdf:type owl:Class .
  filter(
    !(strstarts(str(?class), "http://www.w3.org/2000/01/rdf-schema") ||
      strstarts(str(?class), "http://www.w3.org/2002/07/owl") ||
      strstarts(str(?class), "http://rdfs.org/ns/void") ||
      strstarts(str(?class), "http://www.w3.org/1999/02/22-rdf-syntax-ns"))
  )
  filter not exists {
    ?class sw:term_status ?data .
  }
  filter exists {
    ?subclass a owl:Class ;
           rdfs:subClassOf ?class .
  }
}
