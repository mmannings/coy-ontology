
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xkos: <http://rdf-vocabulary.ddialliance.org/xkos#>
PREFIX json: <http://jsa.aksw.org/fn/json/>
PREFIX csv: <http://jsa.aksw.org/fn/csv/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

PREFIX cpc: <https://data.coypu.org/classification/cpc_v2.1/>
PREFIX hs: <https://data.coypu.org/classification/hs_2012/>
PREFIX nace1: <https://data.coypu.org/classification/nace_r1.1/>
PREFIX isic: <https://data.coypu.org/classification/isic_r4/>

CONSTRUCT {
  ?s a skos:Concept ;
     skos:inScheme nace1: ;
     skos:prefLabel ?label ; 
     skos:notation ?code ;
     skos:broader ?parent ;
     xkos:coreContentNote ?core_content_note ;
     xkos:additionalContentNote ?additional_content_note ;
     xkos:exclusionNote ?exclusion_note 
  
} WHERE {
  VALUES (?url ?id_col ?label_col ?ns) {
    (<env://INPUT> "Code" "Description" nace1:)
  } 
  
  ?url csv:parse (?rowJsonObj "-ho") .
  BIND(json:path(?rowJsonObj, "$." + ?id_col) AS ?code)
  BIND(json:path(?rowJsonObj, "$." + ?label_col) AS ?label_txt)

  BIND(URI(CONCAT(STR(?ns), ?code)) AS ?s)
  BIND(strlang(?label_txt, "en") AS ?label)
  
  # parent concept
  BIND(json:path(?rowJsonObj, "$.Parent") AS ?parent_code)
  BIND(URI(CONCAT(STR(?ns), ?parent_code)) AS ?parent)
  
  BIND(json:path(?rowJsonObj, "$.['This item includes']") AS ?core_txt)
  BIND(IF(STRLEN(?core_txt) > 0, strlang(?core_txt, "en"), COALESCE()) AS ?core_content_note)
  
  BIND(json:path(?rowJsonObj, "$.['This item also includes']") AS ?additional_txt)
  BIND(IF(STRLEN(?additional_txt) > 0, strlang(?additional_txt, "en"), COALESCE()) AS ?additional_content_note)
  
  BIND(json:path(?rowJsonObj, "$.['This item excludes']") AS ?exclusion_txt)
  BIND(IF(STRLEN(?exclusion_txt) > 0, strlang(?exclusion_txt, "en"), COALESCE()) AS ?exclusion_note)
}

